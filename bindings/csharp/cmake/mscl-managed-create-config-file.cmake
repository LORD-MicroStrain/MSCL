set(MSCL_CSHARP_CONFIG_FILE_NAME "${MSCL_CSHARP_MANAGED_TARGET_LOWER}-config")

set(MSCL_CSHARP_VERSION_FILE_NAME "${MSCL_CSHARP_CONFIG_FILE_NAME}-version.cmake")
set(MSCL_CSHARP_VERSION_FILE_OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${MSCL_CSHARP_VERSION_FILE_NAME}")

string(APPEND MSCL_CSHARP_CONFIG_FILE_NAME ".cmake")

set(MSCL_CSHARP_CONFIG_FILE_IN "${CMAKE_CURRENT_LIST_DIR}/${MSCL_CSHARP_CONFIG_FILE_NAME}.in")
set(MSCL_CSHARP_CONFIG_FILE_OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${MSCL_CSHARP_CONFIG_FILE_NAME})

include(CMakePackageConfigHelpers)

# Set the variables used in the file when configure_package_config_file is called
set(PACKAGE_NAME "${MSCL_CSHARP_MANAGED_TARGET_LOWER}")
set(LIBRARY_NAME "${MSCL_CSHARP_MANAGED_TARGET}")
string(TOUPPER "${MSCL_CSHARP_MANAGED_TARGET}" LIBRARY_NAME_UPPER)

# Create the package config file
configure_package_config_file(
    "${MSCL_CSHARP_CONFIG_FILE_IN}"
    "${MSCL_CSHARP_CONFIG_FILE_OUTPUT}"
    INSTALL_DESTINATION
        "${MSCL_CSHARP_CONFIG_INSTALL_DIR}"
)

# Create the package version file
write_basic_package_version_file(
    "${MSCL_CSHARP_VERSION_FILE_OUTPUT}"
    VERSION "${PROJECT_VERSION}"
    COMPATIBILITY AnyNewerVersion
    ARCH_INDEPENDENT
)

set(MSCL_CSHARP_CONFIG_FILES
    "${MSCL_CSHARP_CONFIG_FILE_OUTPUT}"
    "${MSCL_CSHARP_VERSION_FILE_OUTPUT}"
)

# Install all of the configuration files
install(
    FILES ${MSCL_CSHARP_CONFIG_FILES}
    DESTINATION "${MSCL_CSHARP_CONFIG_INSTALL_DIR}"
    COMPONENT "${MSCL_CSHARP_COMPONENT_NAME}"
)
