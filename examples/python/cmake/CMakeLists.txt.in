# NOTE: This is a generated file to make a wrapper to run a Python script using
# CMake as a target

# Supported build configurations
set(CMAKE_CONFIGURATION_TYPES "Release")

if(CMAKE_BUILD_TYPE AND NOT "${CMAKE_BUILD_TYPE}" STREQUAL "${CMAKE_CONFIGURATION_TYPES}")
    set(CMAKE_BUILD_TYPE "${CMAKE_CONFIGURATION_TYPES}")
    message(WARNING "This Python project can only run in ${CMAKE_BUILD_TYPE} mode")
endif()

cmake_minimum_required(VERSION 3.16)
project("MSCL-Python@MSCL_PYTHON_VERSION@-@MSCL_PYTHON_EXAMPLE_TYPE@-Example"
    VERSION "1.0.0"
    DESCRIPTION "Simple @MSCL_PYTHON_EXAMPLES_SUBDIRECTORY@ example using Python"
    LANGUAGES CXX
)

# MSCL defines this for specific versions of Python
if(NOT DEFINED CACHE{Python@MSCL_PYTHON_VERSION@_ROOT_DIR})
    message(FATAL_ERROR "Python@MSCL_PYTHON_VERSION@_ROOT_DIR needs to be set to find the correct Python application")
endif()

if(NOT EXISTS "${Python@MSCL_PYTHON_VERSION@_ROOT_DIR}")
    message(FATAL_ERROR "Python@MSCL_PYTHON_VERSION@_ROOT_DIR needs to be a valid Python interpreter path")
endif()

set(Python_ROOT_DIR ${Python@MSCL_PYTHON_VERSION@_ROOT_DIR})

# Find the Python interpreter
find_program(
    Python_EXECUTABLE
    NAMES "python" "python@MSCL_PYTHON_VERSION@"
    PATHS "${Python_ROOT_DIR}"
    REQUIRED
)

# The example script to run
set(PYTHON_SCRIPT "@CMAKE_CURRENT_LIST_DIR@/@MSCL_PYTHON_EXAMPLES_SUBDIRECTORY@/main.py")

# Create a source file to be able to run the example through CMake and/or an IDE
# This needs to be architecture specific for TARGET_FILE_DIR to work properly
math(EXPR BIT_TYPE "${CMAKE_SIZEOF_VOID_P} * 8")
set(PROJECT_SOURCE "${CMAKE_CURRENT_LIST_DIR}/main-${BIT_TYPE}-bit.cpp")

set_source_files_properties(${PROJECT_SOURCE} PROPERTIES
    GENERATED ON
)

set(MSCL_PYTHON@MSCL_PYTHON_VERSION@_TARGET "@PROJECT_NAME@-Python@MSCL_PYTHON_VERSION@")

# The purpose of this executable is to allow running the Python script directly through CMake or IDEs that support it
add_executable("${PROJECT_NAME}" ${PROJECT_SOURCE})

# Generate a source file to use for the dummy executable
file(GENERATE
    OUTPUT "${PROJECT_SOURCE}"
    CONTENT "#include <stdlib.h>\nint main()\n{\n    $<$<CXX_COMPILER_ID:MSVC>:_>putenv(\"PYTHONPATH=$<TARGET_FILE_DIR:${MSCL_PYTHON@MSCL_PYTHON_VERSION@_TARGET}>\");\n    return system(\"${Python_EXECUTABLE} ${PYTHON_SCRIPT}\");\n}\n"
    TARGET ${PROJECT_NAME}
)

add_dependencies("${PROJECT_NAME}" "${MSCL_PYTHON@MSCL_PYTHON_VERSION@_TARGET}")

# Configure the project in an examples sub-folder for IDEs that support it
if(NOT "${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_CURRENT_LIST_DIR}")
    file(RELATIVE_PATH PROJECT_RELATIVE_PATH "${CMAKE_SOURCE_DIR}" "${CMAKE_CURRENT_LIST_DIR}/..")
    set_target_properties("${PROJECT_NAME}" PROPERTIES
        FOLDER "${PROJECT_RELATIVE_PATH}"
    )
endif()
